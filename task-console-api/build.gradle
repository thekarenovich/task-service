buildscript {
	ext {
		kotlinJvmVersion = '1.8.0'

		protobufVersion = '3.21.7'
		protobufPluginVersion = '0.8.18'
		grpcVersion = '1.43.0'
		grpcKotlinVersion = '1.3.0'
		protoCVersion = '3.20.1'
	}
}

plugins {
	id 'idea'
	id 'java-library'
	id 'org.jetbrains.kotlin.jvm' version "$kotlinJvmVersion"
	id 'com.google.protobuf' version "$protobufPluginVersion"
}

repositories {
	mavenCentral()
}

dependencies {

	implementation "io.grpc:grpc-protobuf:$grpcVersion"
	implementation "io.grpc:grpc-stub:$grpcVersion"
	implementation "io.grpc:grpc-kotlin-stub:$grpcKotlinVersion"
	implementation "com.google.protobuf:protobuf-java:$protobufVersion"
	implementation "com.google.protobuf:protobuf-kotlin:$protobufVersion"

	//Kotlin
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:$protoCVersion"
	}
	generatedFilesBaseDir = "$projectDir/src/generated"
	clean {
		delete generatedFilesBaseDir
	}

	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
		grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpcKotlinVersion:jdk8@jar" }
	}

	generateProtoTasks {
		ofSourceSet("main").forEach { task ->
			task.plugins {
				grpc { option 'lite' }
				grpckt { option 'lite' }
			}
		}
	}
}
